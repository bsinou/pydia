buildscript {

    // Migration in progress - see:
    // https://android-developers.googleblog.com/2024/04/jetpack-compose-compiler-moving-to-kotlin-repository.html
    // And
    // https://developer.android.com/build/migrate-to-catalogs

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath libs.gradle
        classpath libs.kotlin.gradle.plugin
    }
}

plugins {
    alias libs.plugins.ksp.devtools apply false
    alias libs.plugins.android.application apply false
    alias libs.plugins.compose.compiler apply false
    alias libs.plugins.androidx.room apply false
}

// Retrieve local context
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))
retrieveFromEnv(localProperties) // There must be a more elegant way to do this...

// Manage optional local submodules
def sdkKotlinCheck = new File('sdk-kotlin/sdk-kotlin.gradle')
def openApiClientCheck = new File('sdk-openapi/sdk-openapi.gradle')

// Expose custom properties to all modules in the project.
ext {
    githubUrl = 'https://github.com/bsinou/pydia.git'

    // Expose properties retrieved from files to child projects
    configs = localProperties

    useLocalSdkKotlin = sdkKotlinCheck.exists()
    useLocalOpenapiClient = openApiClientCheck.exists()
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

/* Factorise some of the methods used in various sub projects */
// Pre-create generic common pom configuration:
ext.getBasePom = { repoURL ->
    return {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }

        developers {
            developer {
                id "bsinou"
                name "Bruno Sinou"
                email "bruno.sinou@posteo.de"
            }
        }

        scm {
            url repoURL
        }
    }
}

// Override local.properties sensitive values with environment parameter
def static retrieveFromEnv(Properties props) {

    def tmpValue = System.getenv('ANDROID_KEYSTORE_PATH')
    if (tmpValue != null) {
        props['keystore.path'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_KEYSTORE_PWD')
    if (tmpValue != null) {
        props['keystore.pwd'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_SIGNKEY_ALIAS')
    if (tmpValue != null) {
        props['signkey.alias'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_SIGNKEY_PWD')
    if (tmpValue != null) {
        props['signkey.pwd'] = tmpValue
    }
}
