plugins {
    // Build kotlin project
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'

    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/5.0/userguide/publishing_overview.html#publishing_overview
    // id 'maven-publish'
}

group 'org.sinou.pydia'
version '0.1-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

def vendorName = "Bruno Sinou"
def sdkJavaRepoURL = 'https://github.com/bsinou/pydia.git'
def artifactID = 'sdk-kotlin'
def sdkSwaggerSpecVersion = '4.3.1'
def sdkKotlinVersion = ownVersions['kotlin.sdk']

// Also publish sources and Javadoc
java {
    withJavadocJar()
    withSourcesJar()
}

// Skip integration tests by default
test {
    if (System.properties['test.profile'] != 'integration') {
        exclude '**/integration/*'
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        CellsSdk(MavenPublication) {
            from components.java
            groupId 'org.sinou.pydia'
            artifactId 'sdk-kotlin'
            version = sdkKotlinVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provide base java libraries to communicate with the Pydio Cells stack')
                root.appendNode('name', 'Cells SDK for Java')
                root.appendNode('url', 'https://pydio.com')

                root.children().last() + rootProject.getBasePom(sdkJavaRepoURL)
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': 'Cells SDK for Kotlin',
                'Specification-Version': sdkSwaggerSpecVersion,
                'Specification-Vendor': vendorName,
                'Implementation-Title': artifactID,
                'Implementation-Vendor': vendorName,
                'Implementation-Version': sdkKotlinVersion)
    }
}

dependencies {

    api project(":openapi-client")

    // Set of carefully chosen third-party libraries
    // Do not update lightly: see build.gradle sibling file.

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"
    implementation "com.squareup.moshi:moshi-adapters:1.11.0"

    implementation 'io.swagger:swagger-annotations:1.6.9'
    implementation 'com.metaparadigm:json-rpc:1.0'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'org.threeten:threetenbp:1.5.2'

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'commons-codec:commons-codec:1.15'
    // Enable the use of @Nullable and @NonNull in method parameters for cleaner code
    // implementation 'org.jetbrains:annotations:23.0.0'
    // implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.2'
    // When running with android these are provided by the platform
    testImplementation 'com.amazonaws:aws-java-sdk-core:1.11.1030'
    testImplementation 'com.amazonaws:aws-java-sdk-s3:1.11.1030'
    testImplementation project(":openapi-client")

}
